buildscript {
    repositories {
        maven { url "https://files.minecraftforge.net/maven" }
        maven { url "https://repo.spongepowered.org/maven" }
        gradlePluginPortal()
        mavenCentral()
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "4.1.+", changing: true
        classpath "gradle.plugin.com.matthewprenger:CurseGradle:1.4.0"
        classpath "org.spongepowered:mixingradle:0.7-SNAPSHOT"
        classpath "com.github.jengelman.gradle.plugins:shadow:5.0.0"
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

def mcVersion = project.mc_version
def username = project.username
def password = project.password

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "net.minecraftforge.gradle"
apply plugin: "org.spongepowered.mixin"
apply plugin: "eclipse"

import net.minecraftforge.gradle.common.task.SignJar

version = project.version
group = "com.stevekung.originatia"
archivesBaseName = "Originatia"
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = 1.8
println("Java: " + System.getProperty("java.version") + " JVM: " + System.getProperty("java.vm.version") + "(" + System.getProperty("java.vendor") + ") Arch: " + System.getProperty("os.arch"))
compileJava.options.encoding = "UTF-8"

minecraft {
    mappings channel: "snapshot", version: "${mcp_mapping}"
    accessTransformer file("src/main/resources/META-INF/accesstransformer.cfg")

    runs {
        client {
            workingDirectory project.file("run")
            source sourceSets.main
            args "--mixin", "mixins.originatia.json", "--username", username, "--password", password
            jvmArgs "-Dmixin.debug.export=true", "-Dmixin.debug.verbose=true"
        }
    }
}

configurations {
    embed
    compile.extendsFrom(embed)
}

shadowJar {
    //configurations = [project.configurations.embed]
    baseName = "Originatia-" + mcVersion
    classifier = ""
    //relocate "org.apache.commons.text", "com.stevekung.originatia.relocate.org.apache.commons.text"
}

dependencies {
    minecraft "net.minecraftforge:forge:${forge_version}"
    annotationProcessor "org.spongepowered:mixin:0.8.2:processor"
    compile files("libs/SteveKunG's-Lib-${mc_version}-${stevekung_lib_version}-deobf.jar")
    compile files("libs/Indicatia-${mc_version}-${indicatia_version}-deobf.jar")
    /*embed("org.apache.commons:commons-text:1.8") {
        exclude group: "org.apache.commons", module: "commons-lang3"
    }*/
}

repositories {
    maven {
        name "Shadow"
        url "https://jitpack.io"
    }
}

sourceSets {
    main {
        ext.refMap = "mixins.originatia.refmap.json"
    }
}

reobf {
    /*shadowJar {
        dependsOn tasks.createMcpToSrg
        mappings = tasks.createMcpToSrg.outputs.files.singleFile
    }*/
}

jar {
    manifest {
        attributes([
            "MixinConfigs": "mixins.originatia.json",
            "Implementation-Title": "${archivesBaseName}",
            "Implementation-Version": "${version}",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    baseName = "Originatia-" + mcVersion
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "LICENSE.txt", "META-INF/*.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }
    baseName = "Originatia-" + mcVersion
    classifier = "sources"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    from(configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "LICENSE.txt", "META-INF/*.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }
    manifest {
        attributes([
            "MixinConfigs": "mixins.originatia.json",
            "Implementation-Title": "${archivesBaseName}",
            "Implementation-Version": "${version}",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
    baseName = "Originatia-" + mcVersion
    classifier = "deobf"
}

artifacts {
    archives sourcesJar
    archives deobfJar
}

curseforge {
    if (project.hasProperty("upload_api_key")) {
        apiKey = project.upload_api_key
        project {
            id = "434676"
            changelog = file("CHANGE_LOG.md")
            changelogType = "markdown"
            releaseType = "beta"
            addGameVersion mcVersion

            mainArtifact(jar) {
                displayName = "OriginatiaForge-" + mcVersion + "-" + version
                relations {
                    requiredLibrary "stevekungs-lib"
                    optionalLibrary "indicatia"
                }
            }
            addArtifact sourcesJar
            addArtifact deobfJar
        }
    }
}

task signJar(type: SignJar, dependsOn: jar) {
    if (project.hasProperty("upload_api_key")) {
        keyStore = project.keystore_path
        alias = project.alias
        storePass = project.keystore_pass
        keyPass = project.keystore_pass
        inputFile = jar.archivePath
        outputFile = jar.archivePath
    }
}

build.dependsOn signJar
tasks.curseforge.dependsOn(build)